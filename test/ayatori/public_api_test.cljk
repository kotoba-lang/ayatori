(ns ayatori.public-api-test
  (:require [ayatori.agent :as agent]
            [ayatori.discovery :as discovery]
            [ayatori.query :as query]
            [clojure.test :refer [deftest is testing]]
            [kotobase.local :as local]
            [kotobase.store :as st]))

(deftest public-query-entrypoint
  (let [store (local/local-store)]
    (st/-put store "users" "u1" {:name "Alice"})
    (is (= #{["Alice"]}
           (query/query store ["users"]
                        '{:find [?name]
                          :where [[?e :name ?name]]}
                        (constantly true))))))

(deftest public-agent-entrypoint
  (testing "Ayatori exposes the validated vector-to-engine boundary"
    (is (= '{:find [?name]
             :where [[?e "name" ?name]]}
           (agent/->engine-query
            '[:find ?name :where [?e "name" ?name]])))))

(deftest ipni-discovery-keeps-the-content-boundary
  (let [cid "bafy-query-root"
        result (discovery/find-providers
                (fn [_]
                  {:status 200
                   :body {:Providers [{:ID "12D3KooWAyatori"
                                       :Addrs ["/dns4/provider.example/tcp/443/https"]}]}})
                cid
                {:routers ["https://router.example/routing/v1"]})]
    (is (:ok? result))
    (is (= cid (:cid result)))
    (is (= cid (get-in result [:providers 0 :cid])))
    (is (false? (:mutates-cid? result)))))
