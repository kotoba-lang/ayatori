#!/usr/bin/env nbb
(ns build-public-snapshot
  "Build the deterministic Arrangement fixture used by the public IPNI
  benchmark. This performs no network writes: every CID-addressed block and a
  JSON manifest are written below the caller-selected output directory."
  (:require ["fs" :as fs]
            ["path" :as path]
            [arrangement.core :as arr]))

(def corpus-size 12000)
(def partitions
  [{:attr "score"
    :boundaries [1000 2000 3000 4000 5000 6000 7000 8000 9000 10000 11000]
    :budget-bits 4}])

(defn- quads []
  (mapcat (fn [i]
            [{:s (str "entity-" i) :p "score" :o i}
             {:s (str "entity-" i) :p "kind"
              :o (if (zero? (mod i 3000)) "rare" "common")}])
          (range corpus-size)))

(defn- main []
  (let [out (or (first *command-line-args*) "/tmp/ayatori-public-snapshot")
        blocks-dir (path/join out "blocks")
        blocks (atom {})
        put! (fn [cid bytes]
               (swap! blocks assoc cid bytes)
               (js/Promise.resolve cid))
        blind-fn (fn [value] (js/Promise.resolve (pr-str value)))
        crypto-fn (fn [bytes] (js/Promise.resolve bytes))]
    (.mkdirSync fs blocks-dir #js {:recursive true})
    (-> (arr/commit! put!
                     (reduce arr/assert-quad (arr/empty-db) (quads))
                     nil arr/current-schema-version blind-fn crypto-fn partitions)
        (.then
         (fn [snapshot-cid]
           (doseq [[cid bytes] @blocks]
             (.writeFileSync fs (path/join blocks-dir cid) bytes))
           (let [manifest {:schema "ayatori.public-arrangement-fixture/v1"
                           :snapshot-cid snapshot-cid
                           :corpus-size corpus-size
                           :quad-count (* 2 corpus-size)
                           :block-count (count @blocks)
                           :block-bytes (reduce + (map #(.-byteLength %) (vals @blocks)))
                           :partitions partitions
                           :blind "pr-str"
                           :encryption "identity"}]
             (.writeFileSync fs (path/join out "manifest.json")
                             (js/JSON.stringify (clj->js manifest) nil 2))
             (println (js/JSON.stringify (clj->js manifest) nil 2)))))
        (.catch (fn [e]
                  (println (or (.-stack e) (.-message e) (str e)))
                  (set! (.-exitCode js/process) 1))))))

(main)
